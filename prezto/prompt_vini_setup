# Authors:
#   Vinicius Oliveira <me@vinioliveira.com>

function prompt_vini_precmd {
# Replace "/home/<user>" with "~"
  _prompt_vini_pwd="${PWD/#$HOME/~}"

  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

# Get node information.
  if (( $+functions[node-info] )); then
    node-info
  fi
}

rbenv_version() {
  rbenv version 2>/dev/null | awk '{print $1}'
}


function prompt_vini_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to call our function before each command
  add-zsh-hook precmd prompt_vini_precmd

  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- VI-COMMAND ---%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'

  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' (%%B%F{15}%b%f%%b)'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
  #zstyle ':prezto:module:git:info:keys' format 'rprompt' '%b%c%s%D'
  zstyle ':prezto:module:git:info:keys' format \
    'status'  '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{124}[%v]%f'

  # Set node-info
  zstyle ':prezto:module:node:info:version' format '%F{072}(%v)%f'

  # Define prompts.
  PROMPT='%F{132}${_prompt_vini_pwd}%f${${git_info:+${(e)git_info[status]}}}
 %F{blue}% λ %f'

  RPROMPT='${ruby_info[version]} %f${node_info[version]}'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_vini_setup "$@"
