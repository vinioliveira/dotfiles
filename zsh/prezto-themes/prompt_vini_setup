# Authors:
#   Vinicius Oliveira <me@vinioliveira.com>

# Load dependencies.
pmodload 'helper'

_prompt_vini_start_time=$SECONDS

prompt_vini_print_elapsed_time() {
  local end_time=$(( SECONDS - _prompt_vini_start_time ))
  local hours minutes seconds remainder

  if (( end_time >= 3600 )); then
    hours=$(( end_time / 3600 ))
    remainder=$(( end_time % 3600 ))
    minutes=$(( remainder / 60 ))
    seconds=$(( remainder % 60 ))
    print -P "%B%F{red}>>> elapsed time ${hours}h${minutes}m${seconds}s%b"
  elif (( end_time >= 60 )); then
    minutes=$(( end_time / 60 ))
    seconds=$(( end_time % 60 ))
    print -P "%B%F{yellow}>>> elapsed time ${minutes}m${seconds}s%b"
  elif (( end_time > 10 )); then
    print -P "%B%F{green}>>> elapsed time ${end_time}s%b"
  fi
}

function prompt_vini_preexec {
  _prompt_vini_start_time="$SECONDS"
}

function prompt_vini_precmd() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_vini_pwd=$(prompt-pwd)

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby version information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Node version information.
  if (( $+functions[node-info] )); then
    node-info
  fi

  prompt_vini_print_elapsed_time
}


function prompt_vini_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and ruby-info before each command.
  add-zsh-hook preexec prompt_vini_preexec
  add-zsh-hook precmd prompt_vini_precmd

# Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # search for icons https://nerdfonts.com/
  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{6} %f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{9} %f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{14}%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{12}%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%%B%F{8}%f %F{11}%b%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{208}●%f%%b'
  zstyle ':prezto:module:git:info:keys' format 'prompt' ' [$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u]'

# Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{red} v%v%f'

#   # # Set node-info
  zstyle ':prezto:module:node:info:version' format '%F{green}⬢ v%v%f'

# Define prompts.
  PROMPT='%F{5}%n%f at %F{4}%m%f in %F{6}${_prompt_vini_pwd}%f${git_info:+${(e)git_info[prompt]}}
${editor_info[keymap]}'
  RPROMPT='[${ruby_info[version]} %f${node_info[version]}]'
}

prompt_vini_setup "$@"
